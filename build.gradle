apply plugin: 'maven-publish'
defaultTasks 'build'

allprojects {
    group 'eu.decentsoftware.holograms'
    version '2.0'
    description 'Lightweight but powerful hologram plugin with many features and configuration options.'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc) {
        failOnError false
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }

    repositories {
        mavenCentral()
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url = 'https://repo.codemc.io/repository/nms/' }
    }

    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
        filesMatching('version.properties') {
            expand props
        }
    }

    jar {
        dependsOn configurations.runtimeClasspath
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        archivesBaseName = rootProject.name
        from {
            configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
        }
    }
}
