plugins {
    id "maven-publish"
    id "com.gradleup.shadow" version "8.3.6"
}

version = "2.9.0"
description = "A lightweight yet very powerful hologram plugin with many features and configuration options."

repositories {
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "https://repo.codemc.io/repository/maven-public/" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {
    implementation project(":nms")
    implementation project(":nms:nms-common")

    compileOnly "org.spigotmc:spigot:1.13-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.6"
    compileOnly "com.arcaniax:HeadDatabase-API:1.3.2"

    implementation "org.bstats:bstats-bukkit:3.1.0"
    implementation "de.tr7zw:item-nbt-api:2.14.1"
    implementation "com.github.cryptomorin:XSeries:13.0.0"

    compileOnly "org.projectlombok:lombok:1.18.36"
    annotationProcessor "org.projectlombok:lombok:1.18.36"
    compileOnly "org.jetbrains:annotations:26.0.2"
    annotationProcessor "org.jetbrains:annotations:26.0.2"
}

processResources {
    def props = [version: project.version, description: project.description]
    inputs.properties(props)
    filteringCharset = "UTF-8"
    filesMatching("plugin.yml") {
        expand(props)
    }
}

jar {
    manifest {
        attributes "Multi-Release": "true"
    }
}

shadowJar {
    archiveFileName.set("DecentHolograms-${version}.jar")

    relocate "org.bstats", "eu.decentsoftware.holograms.metrics"
    relocate "de.tr7zw.changeme.nbtapi", "eu.decentsoftware.holograms.libs.nbtapi"
    relocate "com.cryptomorin.xseries", "eu.decentsoftware.holograms.libs.xseries"
}

publishing {
    publications {
        create("shadow", MavenPublication) {
            from(components["java"] as SoftwareComponent)
            artifactId = "decentholograms"
        }
    }
}
